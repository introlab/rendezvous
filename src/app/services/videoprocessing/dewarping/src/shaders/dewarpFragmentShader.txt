#version 330 core

#define PI 3.1415926535897932384626433832795

in vec2 textureCoords;

out vec4 out_Color;

uniform sampler2D fisheyeTexture;

uniform float xCenter;
uniform float yCenter;
uniform float dewarpWidth;
uniform float dewarpHeight;
uniform float inRadius;
uniform float centerRadius;
uniform float outRadiusDiff;
uniform float xOffset;
uniform float bottomDistorsionFactor;
uniform float topOffset;
uniform float bottomOffset;

void main(void)
{
	float heightFactor = (1 - ((bottomOffset + topOffset) / dewarpHeight)) * textureCoords.y + topOffset / dewarpHeight;
	ivec2 size = textureSize(fisheyeTexture, 0);
	float factor = outRadiusDiff * sin(PI * textureCoords.x) * sin((PI * heightFactor) / 2.0);
	float radius = textureCoords.y * dewarpHeight + inRadius + factor + (1 - textureCoords.y) * topOffset - textureCoords.y * bottomOffset;
	float theta = ((textureCoords.x * dewarpWidth) + xOffset) / centerRadius;
	out_Color = texture(fisheyeTexture, vec2((xCenter + radius * sin(theta)) / size.x, (yCenter + radius * cos(theta)) / size.y));
}

